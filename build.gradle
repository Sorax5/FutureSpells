import xyz.jpenilla.runtask.task.AbstractRun

plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = "fr.soraxdubbing"
version = "1.8-SNAPSHOT"
description = "FutureSpells"
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven {
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }

    maven {
        url = uri("https://oss.sonatype.org/content/groups/public/")
    }

    maven {
        url = uri("https://jitpack.io")
    }

    maven {
        url = uri("https://repo.maven.apache.org/maven2/")
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT"
    compileOnly "com.github.TheComputerGeek2:MagicSpells:3.5-Release"

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.VERSION_1_8
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-parameters"]
    options.fork = true

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
        options.encoding = 'UTF-8'
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    destinationDirectory.set(file("./builds/"))
}

shadowJar {
    destinationDirectory.set(file("./builds/"))
}

shadowJar {
    if (!project.hasProperty("noRelocation")) {
        relocate("co.aikar.commands", "fr.soraxdubbing.futurespells.acf")
        relocate("co.aikar.locales", "fr.soraxdubbing.futurespells.locales")
    }
}

tasks {
    runServer {
        downloadPlugins {
            github("TheComputerGeek2", "MagicSpells","3.5-Release","MagicSpells-3.5-Release.jar")
            url("https://mediafilez.forgecdn.net/files/3255/724/EffectLib-9.0.jar")
        }
        minecraftVersion("1.12.2")
    }
}

tasks.withType(AbstractRun).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        it.vendor = JvmVendorSpec.JETBRAINS
        it.languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_21.toString())
    }
    jvmArgs += "-XX:+AllowEnhancedClassRedefinition"
}

build.dependsOn shadowJar


